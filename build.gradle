apply plugin: 'java'
apply plugin: 'java-library'

dependencies {

    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.9.1'
    implementation "commons-codec:commons-codec:1.14"
}

repositories {
    mavenCentral()
}

compileJava {
    targetCompatibility = '17'
    sourceCompatibility = '17'
}

test {
    systemProperty "java.library.path", file("${buildDir}/rust/release").absolutePath
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task compileRust(type:Exec) {
    workingDir 'src/rust'
    commandLine 'cargo', 'build', '--release', '--target-dir=../../build/rust'
}

compileJava.dependsOn(compileRust)

jar {
    from("${buildDir}/rust/release") {
        into "native/macos"
        include '*.dylib'
    }
    from("${buildDir}/rust/release") {
        into "native/linux"
        include '*.so'
    }
    from("${buildDir}/rust/release") {
        into "native/windows"
        include '*.dll'
    }
}
